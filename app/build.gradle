plugins {
    id 'java'
    id 'checkstyle'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.4'
    id 'io.spring.dependency-management' version '1.1.3'
}

apply from: 'version.gradle'

group = 'com.mykytaaa'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        name = "GitHubPackages"
        url = "https://maven.pkg.github.com/Mykytaaa/user-profile"
        credentials{
            username = System.getenv("GITHUB_ACTOR")
            password = System.getenv("GITHUB_TOKEN")
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "org.liquibase:liquibase-core:${versions.liquibaseCore}"
    implementation "org.hibernate.validator:hibernate-validator:${versions.hibernateValidator}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${versions.openApiWebMvc}"
    implementation "org.mapstruct:mapstruct:${versions.mapStruct}"
    implementation "org.mapstruct:mapstruct:${versions.mapStruct}"
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-tracing'
    implementation 'io.micrometer:micrometer-tracing-bridge-brave'
    implementation platform("io.micrometer:micrometer-tracing-bom:${versions.micrometerTracing}")
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    runtimeOnly 'org.postgresql:postgresql'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
    testImplementation "org.testcontainers:postgresql:${versions.testContainers}"
    testImplementation 'io.micrometer:micrometer-observation-test'
    testImplementation "org.logcapture:logcapture-core:${versions.logcapture}"
    testImplementation "org.logcapture:logcapture-junit5:${versions.logcapture}"

    compileOnly "org.projectlombok:lombok:${versions.lombok}"
    annotationProcessor "org.projectlombok:lombok"
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'
    annotationProcessor "org.mapstruct:mapstruct-processor:${versions.mapStruct}"
}

tasks.named('test') {
    useJUnitPlatform()
}

jar {
    enabled = false
}

ext {
    isNotTest = project.hasProperty('profile') ? project.property('profile') != 'test' : true
}

bootJar {
    archiveClassifier.set("exec")

    if(isNotTest) {
        exclude '**/changeset/test-data-e2e/**'
    }
}

publishing {
    publications {
        github(MavenPublication) {
            from components.java
            versionMapping {
                allVariants {
                    fromResolutionResult()
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Mykytaaa/user-profile"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}